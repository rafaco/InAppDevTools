apply plugin: 'com.android.library'
apply from: '../gradle/constants.gradle'
apply from: 'preprocess.gradle'
apply from: 'publish.gradle'

android {
    compileSdkVersion PROJECT_COMPILE_SDK
    buildToolsVersion PROJECT_BUILD_TOOLS

    defaultConfig {
        minSdkVersion PROJECT_MIN_SDK
        targetSdkVersion PROJECT_TARGET_SDK
        versionCode PROJECT_VERSION_CODE
        versionName PROJECT_VERSION

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        targetCompatibility PROJECT_TARGET_COMPATIBILITY
        sourceCompatibility PROJECT_SOURCE_COMPATIBILITY
    }

    flavorDimensions "compatibility"
    productFlavors {
        support {
            dimension "compatibility"
            processor.symbols "SUPPORT"
            matchingFallbacks = ['debug', 'release']
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
        androidx {
            dimension "compatibility"
            processor.symbols "ANDROIDX"
            matchingFallbacks = ['debug', 'release']
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // AndroidX flavor
    androidxApi "com.google.android.material:material:$ANDROIDX_MATERIAL_VERSION"
    androidxApi "androidx.appcompat:appcompat:$ANDROIDX_MAIN_VERSION"
    androidxApi "androidx.recyclerview:recyclerview:$ANDROIDX_MAIN_VERSION"
    androidxApi "androidx.cardview:cardview:$ANDROIDX_MAIN_VERSION"
    androidxApi "androidx.media:media:$ANDROIDX_MAIN_VERSION"

    // Support flavor
    supportApi "com.android.support:appcompat-v7:$SUPPORT_MAIN_VERSION"
    supportApi "com.android.support:design:$SUPPORT_MAIN_VERSION"
    supportApi "com.android.support:support-v4:$SUPPORT_MAIN_VERSION"
    supportApi "com.android.support:recyclerview-v7:$SUPPORT_MAIN_VERSION"
    supportApi "com.android.support:cardview-v7:$SUPPORT_MAIN_VERSION"

    //Test
    testImplementation "junit:junit:$JUNIT_VERSION"
    androidTestAndroidxImplementation "androidx.test:runner:$ANDROIDX_TEST_RUNNER"
    androidTestAndroidxImplementation "androidx.test.espresso:espresso-core:$ANDROIDX_TEST_ESPRESSO"
    androidTestImplementation "com.android.support.test:runner:$SUPPORT_TEST_RUNNER"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$SUPPORT_TEST_ESPRESSO"
}

sonarqube {
    androidVariant 'supportDebug'
}
